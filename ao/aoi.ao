std euler

@doc.aoi
"aoi is an imperative REPL for AO, implemented in Haskell. This tool will default to loading the 'aoi' dictionary (i.e. seaching for 'aoi.ao' on AO_PATH). The aoi REPL is NOT intended for long-term use, and it should eventually be replaced by a spreadsheet metaphor with better effects models and proper compilation.
 
 The aoi application model is simplistic. The application starts in a standard environment that is empty except for a linear powerblock. The powerblock has type roughly `µP.[msg→(P*ans)]` where 'msg' is a (label * contents) pair. Use of `iomsg.aoi` will send an ad-hoc message to the powerblock and leave the answer on the stack. All implemented operations also have dedicated words like `readFile`.
 
 The aoi effects model is inadequate for safety, efficiency, scalability, and open system security. Because it is not safe, it also isn't compatible with ambiguity. It is not recommended in general. I would not use it even in aoi, if it were not for how trivial it is to implement as a bootstrap model.
 
 AOI does have a little extra support to help bootstrap a proper REPL/interpreter. See @doc.switchAOI for more information!
~

@doc.iomsg.aoi "invoke linear powerblock with message from stack"
@iomsg.aoi.raw %zwl .apply xf %rwz
@iomsg.aoi xf copy pf put pushFrame iomsg.aoi.raw popFrame

@doc.reload "reload dictionary from filesystem (i.e. to see updates)"
@reload intro1 "reloadDictionary" pf iomsg.aoi elim1

@doc.loadWord "text -- (1 + [x→y]); load word (if it exists) from AOI dictionary"
@loadWord  "loadWord" pf iomsg.aoi

@doc.switchAOI 
"The AOI reader incrementally compiles lines or paragraphs of user input (text) into a blocks that will be applied in the current user environment. Compilation may return error and fail, in which case no action is taken and the text is returned to the user for further editing. The incremental interpreter function has roughly type:
 
     type IFN = (text * eIC) → (errorText + ([eU → eU'] * eIC'))
 
 Here, `eIC` is any extra context the incremental compiler decides to keep from step to step. Type `eU` represents the user's environment. On error, neither eIC nor eU will be modified, and the input is ideally (perhaps via history) returned to the user for further editing. AOI does not currently validate the sanity of the resulting block - i.e. no typechecking internally, no testing against eU. However, the design here makes it feasible to introduce a few features to prevent runtime errors.
 
 Developers may modify the AOI reader (from within eU) by use of switchAOI word and command. 
 
     switchAOI :: ([IFN] * eIC) -- ([IFN]' * eIC')
 
 This is coarse-grained at the level of lines and blocks. The new reader will be used to process the next block. The motivation for switchAOI is to simplify bootstrapping of an AO REPL or interactive mode from within AO.
~ 
@switchAOI "switchAOI" pf iomsg.aoi

@doc.frame.aoi 
"The Haskell AO compiler adds annotations to help track call stack 
 locations for debugging purposes. AOI provides 'pushFrame' and 'popFrame'
 for similar reasons. The input to pushFrame must be simple text.
 
 Be sure to keep popFrame balanced with pushFrame! 
~
@doc.pushFrame "frameText --; see @doc.frame.aoi"
@doc.popFrame "--; see @doc.frame.aoi"
@pushFrame "pushFrame" pf iomsg.aoi.raw elim1
@popFrame intro1 "popFrame" pf iomsg.aoi.raw elim1

@doc.getEnv.iomsg "varname -- text; environment variable or empty text"
@getEnv "getOSEnv" pf iomsg.aoi

@doc.randomBytes "count -- (list of octets); secure random source"
@randomBytes "loadRandomBytes" pf iomsg.aoi

@doc.readFile "filename -- (1(err) + text)"
@doc.readFile.binary "filename -- (1(err) + binary)"
@doc.writeFile "text filename -- (1(err) + 1(ok))"
@doc.writeFile.binary "binary filename -- (1(err) + 1(ok))"
@readFile "readFile" pf iomsg.aoi
@writeFile pf "writeFile" pf iomsg.aoi
@readFile.binary "readFileB" pf iomsg.aoi
@writeFile.binary pf "writeFileB" pf iomsg.aoi

@doc.destroy "destroy will drop any input, including relevant blocks (unsafe)"
@destroy "destroy" pf iomsg.aoi elim1

@doc.clear "clear the current environment (except hand and powerblock)"
@clear introE swapE destroy

@doc.reset "clear the environment then reload the dictionary"
@reset clear reload

@doc.debugOut "val -- val; prints given value on stderr; returns it unchanged"
@debugOut "debugOut" pf iomsg.aoi

@+ .add
@* .multiply
@/ .invert
@~ .negate





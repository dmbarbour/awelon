@doc.Hands
"In AO, the hand is modeled as a second stack. It can be used as a temporary storage for deep operations on the current stack, and also serves as a semantic clipboard for ad-hoc data shuffling.
 
     (currentStack * (hand * ... ))
 
 Developers can:
 
 * take item from stack into hand
 * put top item from hand onto stack
 * data plumbing similar to regular stack
 * discard a value from the hand
 * copy an item from the stack into hand
 * paste an item in the hand onto the stack
 
 In addition, hands are stable when developers use navigational metaphors, i.e. to operate in different workspaces or environments. Also, hands are hidden by some operations, such as `apply`.
 
 AO encourages a "clean hands" policy: most words should have a neutral effect on the hand, and not affect or observe the values already held by it. The exception is words like `take` and `put` where the purpose is to manipulate the hand. Some common words, such as `apply`, will hide the hand - making
~

@take %rzlw
@put %wrzl
@discard put drop
@copy dup take
@paste put copy

@take2 take  take
@take3 take2 take
@take4 take3 take
@take5 take4 take
@take6 take5 take
@take7 take6 take

@put2  put   put
@put3  put2  put
@put4  put3  put
@put5  put4  put
@put6  put5  put
@put7  put6  put

@juggle2 %w roll2 %w
@juggle3 %w roll3 %w
@juggle4 %w roll4 %w

@discard2 discard  discard
@discard3 discard2 discard
@discard4 discard3 discard
@discard5 discard4 discard
@discard6 discard5 discard

@copy2 dup2 take2
@copy3 dup3 take3
@copy4 dup4 take4
@copy5 dup5 take5
@copy6 dup6 take6
@copy7 dup7 take7

@paste2 put2 copy2
@paste3 put3 copy3
@paste4 put4 copy4
@paste5 put5 copy5
@paste6 put6 copy6
@paste7 put7 copy7

@taked swap take
@putd put swap

@doc.take "move value at top of stack to top of hand"
@doc.put "move value at top of hand to top of stack"
@doc.discard "drop and destroy value in hand"
@doc.copy "copy object from top of stack into hand (copy/paste metaphor)"
@doc.paste "paste object from hand onto top of stack (copy/paste metaphor)"
@doc.taked "take second item from stack"
@doc.putd "put below first item on stack"

@reifyHand %w reifyStack %w
@restoreHand %w restoreStack %w
@doc.reifyHand "hand becomes single object in fresh hand"
@doc.restoreHand "single object in hand becomes hand"
@doc.swapHands "swap reified hand on stack with current hand"
@swapHands %rwzwl

@id.takePut take put
@id.putTake put take
@id.juggle3 juggle3 juggle3 juggle3
@id.takeJugglePutSwap take2 juggle2 put2 swap
@id.takeSwapPutSwapd take swap put swapd
@id.copyDiscard copy discard
@id.pasteDiscardTake paste discard take
@id.reifyRestoreHand reifyHand restoreHand
@id.take3put3 take3 put3

@eqv.takeSwapPut.swapd [take swap put] [swapd]


@hx   %w x   %w
@hw   %w w   %w
@hp   %w p   %w
@hwx  %w w x %w
@hpw  %w p w %w
@hl   %w l   %w
@hr   %w r   %w
@hdp  %w dp  %w
@hdx  %w dx  %w
@hdw  %w dw  %w
@hdpw %w dpw %w
@hdwx %w dwx %w
@hdl  %w dl  %w
@hdr  %w dr  %w

@hdup  %w dup %w
@hdupd %w dupd %w
@hover %w over %w
@htuck %w tuck %w
@hpick %w pick %w
@hdrop %w drop %w
@eqv.hdrop.discard [hdrop] [put drop]
@hnip %w nip %w
@hnip2 %w nip2 %w
@hdropd %w dropd %w
@hswap %w swap %w
@hswapd %w swapd %w
@hrot %w rot %w
@hunrot %w unrot %w
@hspin %w spin %w
@hroll %w roll %w
@hunroll %w unroll %w


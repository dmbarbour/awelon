@doc.Lists
"Lists in AO consist of a finite sequence of pairs terminated by a number. For example, a list of four values might have structure:
 
     (a * (b * (c * (d * N(x))))
 
 In type signatures, this structure might be abbreviated as foo`L, and the number ending the list as foo`Nel. Lists are typically processed by asking of each element 'are you a pair?' (a form of dynamic introspection). If so, the first item is an element in the list, and the second is the rest of the list. The terminating number is not an element of the list, but a useful convention of AO is to make use of different numbers to weakly indicate different types, e.g. to support debugging. A few common numbers:
 
     0: generic list terminator
     3: text (list of integers between 0 and 0x10ffff)
     8: binary (list of integers between 0 and 255)
 
 Relevantly, ending with different number does impact comparison functions, and developers may freely assert on list terminals to resist accidents. It is a bad idea to observe a terminal to decide type in any formal sense, but it can be useful in heuristic contexts (e.g. rendering in a debugger).
 
 Lists tend to be reversed every time we process them, and so must be un-reversed when we're done. The list functions tend to include the middle steps with an extra `reverse` in the name. Using the intermediate steps can sometimes be more efficient if one was going to reverse the list again anyway. (Though, this efficiency benefit may eventually vanish if optimizers or ABCD begin to recognize list operations.)
 
 *Aside:* Lists are simplistic, in the sense that they are often overly simple. AO's standard dictionaries should include other data structures more suitable for efficient ad-hoc manipulations. These will include finger-trees, ropes built on finger-trees, and zippers. 
~

@doc.eachT.list "a`L [a --] -- a`Nel; apply block to each element in list, return term"
@doc.reverseT&append.list "d`L s`L -- (reverse s ++ d)`L s`Nel"
@doc.map&reverseT.list "a`L [a -- b] -- b`L(reversed) a`Nel"
@doc.map&reverseT&append.list "b0`L a`L [a--b] -- (b reversed ++ b0)`L a`Nel"
@doc.reverseT.list "a`L -- (reverse a, terminate in 0)`L a`Nel"

@eachT.list [step.eachT.list] bind fixpoint inline
@doc.step.eachT.list "a`L [[a --] step.eachT.list] [a --] -- terminal"
@step.eachT.list
 rot isPair
 [nip2 assertNumber]
 [wx roll p rot dip x inline]
 ifk merged inline

@reverseT&append.list [p] eachT.list
@reverseT.list 0 swap reverseT&append.list
@map&reverseT.list 0 unrot map&reverseT&append.list
@map&reverseT&append.list hidingDest.map.list eachT.list
@hidingDest.map.list [dip] bind [swap] composel [pw] compose

@doc.each.list "a`L [a --] --; apply block to each element in list"
@doc.map.list "a`L [a -- b] -- b`L; apply block modifying each element in list"

@each.list      eachT.list drop
@map.list       map&reverseT.list swap reverseT&append.list drop
@length.list    0 swap [drop inc] each.list

@test.each.list 0 "World" [.add] each.list 520 assertEQ
@test.length.list "hello" length.list 5 assertEQ
@test.map.list "gdkkn" [inc] map.list "hello" assertEQ

@each each.list
@map  map.list
